apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.github.ben-manes.versions'
//gradle dependencyUpdates -Drevision=release

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionName "2.4.2"
        versionCode 2420
        resValue "string", "app_git_hash", getCurrentGitHash()
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        playStore
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            if (signingConfigs.playStore != null) {
                signingConfig signingConfigs.playStore
            }
        }
    }
}

def getVersionCode() {
    return 'git rev-list --count HEAD'.execute().text.toInteger()
}

def getCurrentGitHash() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

def Properties props = new Properties()
def propFile = file('../signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('SIGN_FILE') && props.containsKey('SIGN_KEYSTORE_PASS') &&
            props.containsKey('SIGN_KEYSTORE_ALIAS') && props.containsKey('SIGN_KEYSTORE_ALIAS_PASS')) {

        println 'RELEASE BUILD SIGNING'

        android.signingConfigs.playStore.storeFile = file(props['SIGN_FILE'])
        android.signingConfigs.playStore.storePassword = props['SIGN_KEYSTORE_PASS']
        android.signingConfigs.playStore.keyAlias = props['SIGN_KEYSTORE_ALIAS']
        android.signingConfigs.playStore.keyPassword = props['SIGN_KEYSTORE_ALIAS_PASS']
    } else {
        println 'RELEASE BUILD NOT FOUND SIGNING PROPERTIES'

        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'RELEASE BUILD NOT FOUND SIGNING FILE'
    android.buildTypes.release.signingConfig = null
}



dependencies {
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:support-v4:22.1.1'
    compile 'com.android.support:palette-v7:22.1.1'
    compile 'com.android.support:recyclerview-v7:22.1.1'
    compile 'com.android.support:cardview-v7:22.1.1'

    compile 'com.google.android.gms:play-services-analytics:7.3.0'


    debugCompile project(path: ':sdks:GithubAndroidSdk', configuration: 'debug')
    releaseCompile project(path: ':sdks:GithubAndroidSdk', configuration: 'release')

    // Submodules
    compile project(':libraries:android-floating-action-button')

    compile project (':libraries:breadcrumbs')

    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.2'

    compile 'com.afollestad:material-dialogs:0.7.3.3'

    compile 'se.emilsjolander:stickylistheaders:2.5.2'

    compile 'com.nineoldandroids:library:2.4.0'

    compile 'com.mikepenz.iconics:octicons-typeface:2.2.0@aar'

    compile 'com.github.d-max:spots-dialog:0.3'

    compile 'com.hannesdorfmann.smoothprogressbar:library:1.0.0'

    compile 'com.squareup:otto:1.3.6'

    compile 'com.squareup.dagger:dagger:1.2.2'

    provided 'com.squareup.dagger:dagger-compiler:1.2.1'

    compile('com.mikepenz.materialdrawer:library:2.9.2@aar') {
        transitive = true
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.3.2@aar') {
        transitive = true;
    }
    compile('com.mikepenz.aboutlibraries:library:4.7.2@aar') {
        transitive = true
    }

    compile 'com.github.mrengineer13:snackbar:1.1.0'

    compile 'com.wefika:flowlayout:0.4.0'
}
